module Log where

import Base64

parameter

    /** 
        This defines the output size of the hash function in bytes. For our use
        it is expected we will use SHA256 and this will be instantiated as 32
    */

    type HashSizeBytes : #

    /**
        We cannot use a hash function that outputs a stream
    */
    type constraint fin HashSizeBytes

    /**
        The hash size must be bigger than the output of the CBC-MAC used on
        the initial message
    */
    type constraint HashSizeBytes >= MACOutputBytes

    /**
        Our hash function must take in an arbitrary number
        of bytes (n) and output the hash size of bytes
    */
    hash : {n} (fin n) => [n][8] -> [HashSizeBytes][8]

    //cbcMac block size in bytes 
    //TODO: the cryptol type-checker chockes on this given the type constraints later in the file
    //type CbcMACBlockSizeBytes : #
    //type constraint CbcMACBlockSizeBytes >= 1
    //type constraint fin CbcMACBlockSizeBytes

    /**
        We also need the CBC-MAC function.
    */
    
    cbcMAC : {msgSizeBytes, keySizeBytes} (fin msgSizeBytes, fin keySizeBytes, msgSizeBytes % CbcMACBlockSizeBytes == 0) =>
        [keySizeBytes][8] ->
        [msgSizeBytes][8] ->
        [MACOutputBytes][8] //cbcMAC with AES always has 128 bit output, regardless of keysize

type MACOutputBytes = 16
type CbcMACBlockSizeBytes = 16

/**
    A log entry. The msgHash should always be the hash of the message, unless
    it's the first entry in which case it should be the MAC of the padded
    message
*/
type Log sizeBytes = {
    message : [sizeBytes][8],
    msgHash : [HashSizeBytes][8],
    logMAC  : [16][8]
}

spaceChar = ' '

/**
  The smallest multiple of CbcMACBlockSizeBytes that is strictly bigger than `a+b`
*/
type msgAndHashASCIILen a b = (CbcMACBlockSizeBytes * ((a + b + 1) /^ CbcMACBlockSizeBytes))

msgAndHashASCII : {sizeBytes} (fin sizeBytes) => [sizeBytes][8] -> [HashSizeBytes][8] -> [msgAndHashASCIILen sizeBytes (Base64Length HashSizeBytes)][8]
msgAndHashASCII msg msgHash = msg # (repeat spaceChar) # (encodeBase64 msgHash)

serializeBinaryLog : {sizeBytes} (fin sizeBytes) =>
    Log sizeBytes -> [sizeBytes + HashSizeBytes + 16][8]
serializeBinaryLog log = log.message # log.msgHash # log.logMAC

/** 
    In ASCII format, first come the message and message hash in the same format
    as used to produce the MAC, then a space character, then the base64
    encoding of the MAC, and finally \r\n
*/
serializeASCIILog : {maxMsgSizeBytes} (fin maxMsgSizeBytes) =>
    Log maxMsgSizeBytes -> [Base64Length MACOutputBytes + 3 + msgAndHashASCIILen maxMsgSizeBytes (Base64Length HashSizeBytes)][8]
serializeASCIILog log = (msgAndHashASCII log.message log.msgHash) # [' '] # (encodeBase64 log.logMAC) # ['\r'] # ['\n']

/**
    A log file is a series of log messages. We must
    specify a maximum log size so that all of the entries
    can have the same type
*/
type LogFile maxSizeBytes msgs = [msgs](Log maxSizeBytes)

serializeBinaryLogFile : {maxSizeBytes, msgs} (fin maxSizeBytes, fin msgs, msgs >= 1) =>
    LogFile maxSizeBytes msgs ->
    [msgs * (maxSizeBytes + HashSizeBytes + 16)][8]
serializeBinaryLogFile logFile = join serializedLogs where
    serializedLogs = [serializeBinaryLog entry | entry <- logFile]

serializeASCIILogFile : {maxSizeBytes, msgs} (fin maxSizeBytes, fin msgs, msgs >= 1) =>
    LogFile maxSizeBytes msgs ->
    [msgs * (Base64Length MACOutputBytes + 3 + msgAndHashASCIILen maxSizeBytes (Base64Length HashSizeBytes))][8]
serializeASCIILogFile logFile = join serializedLogs where
    serializedLogs = [serializeASCIILog entry | entry <- logFile]

//Message will be something from secure boot, not identified yet
initialLogEntry : {msgSize, keySize, maxMsgSize}
                (fin msgSize, fin keySize, fin maxMsgSize, maxMsgSize % CbcMACBlockSizeBytes == 0, msgSize <= maxMsgSize) => 
    [keySize][8] -> [msgSize][8] -> Log maxMsgSize
initialLogEntry key msg = 
    { message = paddedMsg, msgHash = msgHash, logMAC = cbcMAC key (msgAndHashASCII paddedMsg msgHash)} where
        paddedMsg = ((msg # (repeat spaceChar)) : [maxMsgSize][8]) // TODO: is the padding with spaces okay or should it be zeros?
        msgHash = (cbcMAC key paddedMsg) # zero // TODO: the use of cbcMAC implies that maxMsgSize is a multiple of 16; I added this as a type constraint, but should we pad `paddedMsg` again instead, and if so with what? Moreover, is the final zero padding okay or should it be spaces?

/**
    Utility function to get the previous hash
*/
getLastHash : {sizeBytes, msgs} (fin msgs, msgs >= 1) => LogFile sizeBytes msgs -> [HashSizeBytes][8]
getLastHash logs = (logs ! 0).msgHash

/**
    Update a log with a new entry
    We require the number of messages to be >= 1 because
    this operation should not be used for the first message
*/
updateLog : {msgSize, keySize, maxMsgSize, msgs}
    (fin msgSize, fin keySize, msgSize <= maxMsgSize, fin msgs, fin maxMsgSize, msgs >=1) =>
    (LogFile maxMsgSize msgs) ->
    [keySize][8] ->
    [msgSize][8] ->
    LogFile maxMsgSize (msgs + 1)
updateLog log key msg = log # [newEntry] where
    paddedMsg = msg # (repeat spaceChar)
    previousHash = getLastHash log
    newEntry = {message = paddedMsg, msgHash = msgHash, logMAC = (cbcMAC key (msgAndHashASCII paddedMsg msgHash))}
    msgHash = hash (paddedMsg # previousHash)

/**
    Given a log entry, check to see if it is a valid first entry
*/
validFirstEntry : {maxMsgSizeBytes, keySizeBytes}
    (fin maxMsgSizeBytes, fin keySizeBytes, maxMsgSizeBytes % CbcMACBlockSizeBytes == 0) =>
    Log maxMsgSizeBytes ->
    [keySizeBytes][8] ->
    Bit
validFirstEntry log key = log.msgHash == (cbcMAC key log.message) # zero /\ log.logMAC == cbcMAC key (msgAndHashASCII log.message log.msgHash)
/* NOTE: the CBC MAC part of this check need not be performed on the smart ballot box */

/**
    Check to see if a log entry is valid given the previous hash
*/
validLogEntry : {msgSizeBytes, keySize}
    (fin msgSizeBytes, fin keySize) =>
    Log msgSizeBytes ->
    [keySize][8] ->
    [HashSizeBytes][8] ->
    Bit
validLogEntry log key prevHash =
    log.msgHash == hash (log.message # prevHash) /\ log.logMAC == cbcMAC key (msgAndHashASCII log.message log.msgHash)
/* NOTE: the CBC MAC part of this check need not be performed on the smart ballot box */

/**
Check the validity of an entire log file relative to a key
*/
validLogFile : {maxMsgSize, keySize, msgs}
    (fin maxMsgSize, fin keySize, fin msgs, maxMsgSize % CbcMACBlockSizeBytes == 0) =>
    LogFile maxMsgSize msgs ->
    [keySize][8] ->
    Bit
validLogFile logFile key =
    `msgs == (0 : [width msgs]) \/
    (foldResult ! 0 where
        foldResult = [validFirstEntry (logFile @ 0) key] #
                        [prevValid /\ validLogEntry log key (prevLog.msgHash)
                            | log <- logFile
                            | prevLog <- [zero] # logFile
                            | prevValid <- foldResult])
